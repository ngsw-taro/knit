buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'

def VERSION = '0.1.2'
group 'com.taroid.knit'
version VERSION

bintray {
    def propertiesFile = rootProject.file('local.properties')
    if (propertiesFile.exists()) {
        Properties properties = new Properties()
        properties.load(propertiesFile.newDataInputStream())
        user = properties.getProperty('bintray.user')
        key = properties.getProperty('bintray.apiKey')
    } else {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_API_KEY')
    }

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'knit'
        desc = 'JUnit API set for Kotlin'
        licenses = ['Apache-2.0']
        labels = ['kotlin', 'junit']
        websiteUrl = 'https://github.com/ntaro/knit'
        vcsUrl = 'https://github.com/ntaro/knit.git'
        publicDownloadNumbers = true

        githubRepo = 'ntaro/knit'
        githubReleaseNotesFile = 'README.md'

        version {
            name = VERSION
            released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
        }
    }
}

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile 'junit:junit:4.12'
}
